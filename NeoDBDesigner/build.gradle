/*
* Gradle Build File by Chris Deter
*/
import org.gradle.plugins.ide.eclipse.model.AccessRule
import org.gradle.plugins.ide.eclipse.model.SourceFolder

plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'application'
	id 'maven-publish'
	id 'antlr'
	id "com.diffplug.gradle.spotless" version "3.23.0"
}

def main = "de.mach.tools.neodesigner.ui.Starter"
def name = 'Mach-ER'
// Vorsicht beim 채ndern der Version, Artifakt wird danach evtl. nicht mehr im Repository gefunden
def vers = '1.4.1'  + '-SNAPSHOT'
def antlrGenSrc = "${project.buildDir}/generated-src/antlr/main/de/mach/tools/neodesigner/inex/nimport/antlrsql/"

group = 'de.mach.tools'
sourceCompatibility = 1.8
version = vers
mainClassName = main
sourceSets.main.antlr.srcDirs = ['src/main/antlr/de/mach/tools/neodesigner/inex/nimport/antlrsql/']

dependencies {
	// Logging:
	// SLF4J Simple Logging Facade for Java (MIT License)
	implementation "org.slf4j:slf4j-simple:1.7.25"

	// GUI Autovervollst채ndigung, Live Feldpr체fung & Notifications
	// ControlsFX (BSD-3 License)
	implementation "org.controlsfx:controlsfx:8.40.14"

	// TEX Export:
	// Apache Velocity (Apache License Version 2.0)
	implementation 'org.apache.velocity:velocity-engine-core:2.0'

	// Neo4j Import & Export:
	// Neo4j Java Driver (Apache License Version 2.0)
	implementation "org.neo4j.driver:neo4j-java-driver:1.7.+"

	// SQL Import:
	// ANTLR (BSD License)
	implementation "org.antlr:antlr4-runtime:4.7.1"

	// GSON f체r JSON Import & Export
	implementation 'com.google.code.gson:gson:2.8.5'

	implementation 'com.beust:jcommander:1.72'

	// Darstellung von Graphen (Tabelle, Kategorien und gesammtes Datenmodell)
	// Graphstream (LGPL)
	implementation "org.graphstream:gs-core:1.3"
	implementation "org.graphstream:gs-ui:1.3"
	implementation "org.graphstream:gs-algo:1.3"

	// TestFX
	testImplementation 'org.testfx:testfx-core:4.0.15-alpha'
	testImplementation 'org.testfx:testfx-junit5:4.0.15-alpha'

	// JUnit test framework
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.+'
	testImplementation 'org.junit.vintage:junit-vintage-engine:5.+'

	// Generate Antlr Code
	antlr "org.antlr:antlr4:4.7.1"
}

jar {
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
	manifest {
		attributes  'Implementation-Title': name,
					'Implementation-Version': vers,
					'Main-Class': main
	}
}

// Update Gradle
task buildWrapper(type: Wrapper) {
	gradleVersion = '5.2.1'
}

artifacts {
	archives jar
}

test {
	useJUnitPlatform()
}

// In this section you declare where to find the dependencies of your project
repositories {
	mavenCentral()
	jcenter()
}

// ------- Antlr Configuration -------
generateGrammarSource {
	maxHeapSize = "64m"
	arguments += ['-package', 'de.mach.tools.neodesigner.inex.nimport.antlrsql']
	outputDirectory = new File(antlrGenSrc)
}
compileJava.dependsOn generateGrammarSource

// ------- Include Artifact publishing for MACH Toolchain -------
// apply from: 'publish-mach.gradle'

// ------- Code Formatter SETUP -------
spotless {
	format 'misc', {
		target '**/*.gradle', '**/*.md', '**/.gitignore'

		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		licenseHeaderFile 'spotless.license.java'		// License header file
		importOrderFile 'spotless.importorder'	// A sequence of package names
		removeUnusedImports() // removes any unused imports
		eclipse().configFile 'spotless.eclipseformat.xml'	// XML file dumped out by the Eclipse formatter
		encoding 'utf-8'
	}
}

afterEvaluate {
	tasks.getByName('spotlessCheck').dependsOn(tasks.getByName('spotlessApply'))
}

// ------- Eclipse IDE SETUP -------
eclipse.classpath {
	file.whenMerged {
		// Fix: Sourcefolder ANTLR
		cp -> cp.entries.add( new SourceFolder(antlrGenSrc, null) )

		//Java FX Packages Workaround
		entries.each { source ->
			if (source.kind == 'con' && source.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER')) {
				source.accessRules.add(new AccessRule('0', 'javafx/**'))
			}
		}
	}
}

// ------- IntelliJ IDE SETUP -------
idea {
	module {
		// Marks the already(!) added srcDir as "generated"
		generatedSourceDirs += file(antlrGenSrc)
	}
}
