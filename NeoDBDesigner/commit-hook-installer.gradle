// Test fuer automatische Git Commit Hook Verteilung zu testen

task copyCommitHook(type: Copy) {
	group 'git hooks'
	description 'Copies the git hooks from team-props/git-hooks to the .git folder.'
	from("${rootDir}/tools/hooks") {
		include '**/*.sh'
		rename '(.*).sh', '$1'
	}
	into "${rootDir}/.git/hooks"
	// onlyIf { isLinuxOrMacOs() }
}

task installGitHooks(type: Exec) {
	description 'Installs the pre-commit git hooks from team-props/git-hooks.'
	group 'git hooks'
	workingDir rootDir
	commandLine 'chmod'
	args '-R', '+x', '.git/hooks/'
	dependsOn copyCommitHook
	// onlyIf { isLinuxOrMacOs() }
	doLast {
		logger.info('Git hook installed successfully.')
	}
}

static def isLinuxOrMacOs() {
	def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
	return osName.contains('linux') || osName.contains('mac os') || osName.contains('macos')
}

afterEvaluate {
	// We install the hook at the first occasion
	tasks['clean'].dependsOn copyCommitHook
	tasks['assemble'].dependsOn copyCommitHook
}
